apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.artifact = 'androiddatetimeformatters-javatime'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        versionName rootProject.ext.libraryVersion

        testInstrumentationRunner 'drewhamilton.androiddatetimeformatters.javatime.MultiDexAndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'

        // Support core library desugaring:
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
}

apply from: 'publish.gradle'

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.4'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation project(':test')
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

// TODO WORKAROUND: Get this to work in its own file
//region Compatibility
configurations {
    baseline
    latest
}

dependencies {
    baseline("drewhamilton.androiddatetimeformatters:$artifact:$compatibleVersion") {
        transitive = false
        force = true
    }
    latest project(path: ":$project.name", configuration: 'releaseRuntimeElements')
}

apply plugin: 'me.champeau.gradle.japicmp'
//noinspection UnnecessaryQualifiedReference
task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath = configurations.baseline.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    newClasspath = configurations.latest.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    onlyBinaryIncompatibleModified = true
    failOnModification = true
    txtOutputFile = file("$buildDir/reports/japi.txt")
}

check.dependsOn(japicmp)
build.dependsOn(check)
//endregion
