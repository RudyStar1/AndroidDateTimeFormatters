buildscript {
    ext {
        versions = [
            kotlin: '1.3.71'
        ]
        deps = [
            androidTestRules: 'androidx.test:rules:1.2.0',
            desugarJdkLibs: 'com.android.tools:desugar_jdk_libs:1.0.5',
            junit: 'junit:junit:4.13',
            junitAndroid: 'androidx.test.ext:junit:1.1.1',
            kotlinStdLib: "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
            multidex: 'com.android.support:multidex:1.0.3',
            threeTenAbp: 'com.jakewharton.threetenabp:threetenabp:1.2.3',
            threeTenBp: 'org.threeten:threetenbp:1.4.3:no-tzdb',
        ]
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-beta04'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"

        classpath 'me.champeau.gradle:japicmp-gradle-plugin:0.2.8'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }

    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }
}

allprojects { project ->
    repositories {
        google()
        jcenter()
    }

    //noinspection UnnecessaryQualifiedReference
    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        project.android.libraryVariants.all { variant ->
            // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
            variant.generateBuildConfigProvider.get().enabled = false
        }
    }
}

ext {
    libraryVersion = '1.0.0'
    compatibleVersion = '1.0.0-beta1'

    minSdkVersion = 15
    compileSdkVersion = 28

    keystore = project.hasProperty('personalKeystore') ? personalKeystore : 'dummy'
    keystorePassword = project.hasProperty('personalKeystorePassword') ? personalKeystorePassword : 'dummy'
    keyAlias = project.hasProperty('personalKeyAlias') ? personalKeyAlias : 'dummy'
    keyPassword = project.hasProperty('personalKeyPassword') ? personalKeyPassword : 'dummy'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
