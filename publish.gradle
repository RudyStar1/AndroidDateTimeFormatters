// Applied per published module
//  The module must define ext.artifactName and ext.publishedDescription

group = 'dev.drewhamilton.androidtime'
version = rootProject.ext.libraryVersion

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        classpath += variant.javaCompileProvider.get().classpath
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

apply plugin: 'maven-publish'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId artifactName
                version version

                from components.release
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = artifactName
                    description = publishedDescription

                    url = 'https://github.com/drewhamilton/AndroidDateTimeFormatters'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'drewhamilton'
                            name = 'Drew Hamilton'
                            email = 'drew.hamilton.0+android@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/drewhamilton/AndroidDateTimeFormatters.git'
                        developerConnection = 'scm:git:ssh://github.com/drewhamilton/AndroidDateTimeFormatters.git'
                        url = 'https://github.com/drewhamilton/AndroidDateTimeFormatters'
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'MavenCentral'

                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                def sonatypeIssuesUsername = project.hasProperty('personalSonatypeIssuesUsername')
                    ? personalSonatypeIssuesUsername
                    : 'x'
                def sonatypeIssuesPassword = project.hasProperty('personalSonatypeIssuesPassword')
                    ? personalSonatypeIssuesPassword
                    : 'x'
                credentials {
                    username sonatypeIssuesUsername
                    password sonatypeIssuesPassword
                }
            }
        }
    }
}

apply plugin: 'signing'
ext.'signing.keyId' = hasProperty('personalGpgKeyId') ? personalGpgKeyId : 'x'
ext.'signing.password' = hasProperty('personalGpgPassword') ? personalGpgPassword : 'x'
ext.'signing.secretKeyRingFile' = hasProperty('personalGpgKeyringFile') ? personalGpgKeyringFile : 'x'
signing {
    sign publishing.publications
}
